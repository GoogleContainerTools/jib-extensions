plugins {
  id 'java-gradle-plugin'
  id 'net.researchgate.release'
  id 'maven-publish'
}

dependencies {
  compileOnly "com.google.cloud.tools:jib-gradle-plugin-extension-api:${dependencyVersions.JIB_GRADLE_EXTENSION}"
  compileOnly "com.google.guava:guava:${dependencyVersions.GUAVA}"

  testImplementation "com.google.cloud.tools:jib-gradle-plugin-extension-api:${dependencyVersions.JIB_GRADLE_EXTENSION}"
  testImplementation "junit:junit:${dependencyVersions.JUNIT}"
  testImplementation "org.mockito:mockito-core:${dependencyVersions.MOCKITO_CORE}"
  testImplementation "com.google.truth:truth:${dependencyVersions.TRUTH}"
  testImplementation "com.google.truth.extensions:truth-java8-extension:${dependencyVersions.TRUTH8}"
  testImplementation "pl.pragmatists:JUnitParams:${dependencyVersions.JUNIT_PARAMS}"
}


jar {
  manifest {
    attributes 'Implementation-Version': version
    attributes 'Automatic-Module-Name': 'com.google.cloud.tools.jib.gradle.extension.classpathmainclass'

    // OSGi metadata
    attributes 'Bundle-SymbolicName': 'com.google.cloud.tools.jib.gradle.extension.classpathmainclass'
    attributes 'Bundle-Name': 'Classpath and Main Class Extension for Jib Gradle Plugin'
    attributes 'Bundle-Vendor': 'Google LLC'
    attributes 'Bundle-DocURL': 'https://github.com/GoogleContainerTools/jib-extensions'
    attributes 'Bundle-License': 'https://www.apache.org/licenses/LICENSE-2.0'
    attributes 'Export-Package': 'com.google.cloud.tools.jib.*'
  }
}

/* RELEASE */
configureMavenRelease()

publishing {
  publications {
    mavenJava(MavenPublication) {
      pom {
        name = 'Classpath and Main Class Extension for Jib Gradle Plugin'
        description = 'Enables extracting and handling Java classpath and main class in default image entrypoint.'
      }
      from components.java
    }
  }
}

// Release plugin (git release commits and version updates)
release {
  tagTemplate = 'v$version-jib-classpath-main-class-extension-gradle'
  git {
    requireBranch = /^jib-classpath-main-class-extension-gradle-release-v\d+.*$/  //regex
  }
}
/* RELEASE */
