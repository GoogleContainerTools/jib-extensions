plugins {
  id 'java-gradle-plugin'
  id 'net.researchgate.release'
  id 'maven-publish'
}

repositories {
  gradlePluginPortal()
}

dependencies {
  compileOnly "com.google.cloud.tools:jib-gradle-plugin-extension-api:${dependencyVersions.JIB_GRADLE_EXTENSION}"
  compileOnly "gradle.plugin.com.google.cloud.tools:jib-gradle-plugin:${dependencyVersions.JIB_GRADLE}"
  compileOnly "com.google.guava:guava:${dependencyVersions.GUAVA}"
  compileOnly "org.springframework.boot:spring-boot-gradle-plugin:${dependencyVersions.SPRING_BOOT}"

  testImplementation "com.google.cloud.tools:jib-gradle-plugin-extension-api:${dependencyVersions.JIB_GRADLE_EXTENSION}"
  testImplementation "gradle.plugin.com.google.cloud.tools:jib-gradle-plugin:${dependencyVersions.JIB_GRADLE}"
  testImplementation "com.google.guava:guava:${dependencyVersions.GUAVA}"
  testImplementation "junit:junit:${dependencyVersions.JUNIT}"
  testImplementation "org.mockito:mockito-core:${dependencyVersions.MOCKITO_CORE}"
  testImplementation "org.springframework.boot:spring-boot-gradle-plugin:${dependencyVersions.SPRING_BOOT}"
}

jar {
  manifest {
    attributes 'Implementation-Version': version
    attributes 'Automatic-Module-Name': 'com.google.cloud.tools.jib.gradle.extension.springboot'

    // OSGi metadata
    attributes 'Bundle-SymbolicName': 'com.google.cloud.tools.jib.gradle.extension.springboot'
    attributes 'Bundle-Name': 'Spring Boot Extension for Jib Gradle Plugin'
    attributes 'Bundle-Vendor': 'Google LLC'
    attributes 'Bundle-DocURL': 'https://github.com/GoogleContainerTools/jib-extensions'
    attributes 'Bundle-License': 'https://www.apache.org/licenses/LICENSE-2.0'
    attributes 'Export-Package': 'com.google.cloud.tools.jib.*'
  }
}

/* RELEASE */
configureMavenRelease()

publishing {
  publications {
    mavenJava(MavenPublication) {
      pom {
        name = 'Spring Boot Extension for Jib Gradle Plugin'
        description = 'Provides extended support for Spring Boot projects.'
      }
      from components.java
    }
  }
}

// Release plugin (git release commits and version updates)
release {
  tagTemplate = 'v$version-jib-spring-boot-extension-gradle'
  git {
    requireBranch = /^jib-spring-boot-extension-gradle-release-v\d+.*$/  //regex
  }
}
/* RELEASE */
