plugins {
  id 'net.researchgate.release'
  id 'maven-publish'
}

dependencies {
  compileOnly "com.google.cloud.tools:jib-maven-plugin-extension-api:${dependencyVersions.JIB_MAVEN_EXTENSION}"
  compileOnly "com.google.guava:guava:${dependencyVersions.GUAVA}"

  testImplementation "com.google.cloud.tools:jib-maven-plugin-extension-api:${dependencyVersions.JIB_MAVEN_EXTENSION}"
  testImplementation "junit:junit:${dependencyVersions.JUNIT}"
  testImplementation "org.mockito:mockito-core:${dependencyVersions.MOCKITO_CORE}"
}

jar {
  manifest {
    attributes 'Implementation-Version': version
    attributes 'Automatic-Module-Name': 'com.google.cloud.tools.jib.maven.extension.layerfilter'

    // OSGi metadata
    attributes 'Bundle-SymbolicName': 'com.google.cloud.tools.jib.maven.extension.layerfilter'
    attributes 'Bundle-Name': 'Layer Filter Extension for Jib Maven Plugin'
    attributes 'Bundle-Vendor': 'Google LLC'
    attributes 'Bundle-DocURL': 'https://github.com/GoogleContainerTools/jib-extensions'
    attributes 'Bundle-License': 'https://www.apache.org/licenses/LICENSE-2.0'
    attributes 'Export-Package': 'com.google.cloud.tools.jib.*'
  }
}

/* RELEASE */
configureMavenRelease()

publishing {
  publications {
    mavenJava(MavenPublication) {
      pom {
        name = 'Layer Filter Extension for Jib Maven Plugin'
        description = 'Allows fine-grained control of files in image layers.'
      }
      from components.java
    }
  }
}

// Release plugin (git release commits and version updates)
release {
  tagTemplate = 'v$version-jib-layer-filter-extension-maven'
  git {
    requireBranch = /^jib-layer-filter-extension-maven-release-v\d+.*$/  //regex
  }
}
/* RELEASE */
